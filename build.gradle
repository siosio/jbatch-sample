group 'siosio.jbatch'
version '1.0-SNAPSHOT'

buildscript {
  ext {
    kotlin_version = "1.0.1-2"
  }
  repositories {
    mavenCentral()
  }
  dependencies {
    classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:${kotlin_version}"
  }
}

subprojects {

  apply plugin: 'idea'
  apply plugin: 'kotlin'

  if (project.name != 'common') {
    apply plugin: 'application'
    mainClassName = "org.jberet.se.Main"
  }

  sourceCompatibility = 1.8

  repositories {
    mavenLocal()
    mavenCentral()
    maven { url "https://repository.jboss.org/nexus/content/repositories/releases/" }
    maven { url 'http://repo.gradle.org/gradle/libs-releases-local' }
  }

  configurations {
    provided
  }

  sourceSets.main.compileClasspath += configurations.provided
  sourceSets.test.compileClasspath += configurations.provided

  sourceSets {
    main {
      output.classesDir = output.resourcesDir
    }
  }

  dependencies {
    compile 'siosio:kotlin-sql:+'
    // jberet
    compile 'org.jberet:jberet-core:1.3.0.Beta1'
    compile 'org.jberet:jberet-se:1.3.0.Beta1'
    compile 'org.jboss.spec.javax.batch:jboss-batch-api_1.0_spec:1.0.0.Final'
    compile 'javax.inject:javax.inject:1'

    compile 'org.jboss.marshalling:jboss-marshalling:1.4.10.Final'
    compile 'org.wildfly.security:wildfly-security-manager:1.1.2.Final'
    compile 'org.jboss.spec.javax.transaction:jboss-transaction-api_1.2_spec:1.0.1.Final'

    // weld
    compile 'org.jboss.weld:weld-core:2.3.4.Final'
    compile 'org.jboss.weld.se:weld-se:2.3.4.Final'

    // h2
    compile 'com.h2database:h2:1.4.191'

    // log
    compile 'org.slf4j:slf4j-api:1.7.21'
    runtime 'ch.qos.logback:logback-classic:1.1.7'

    // kotlin
    compile "org.jetbrains.kotlin:kotlin-stdlib:${kotlin_version}"

    // test
    testCompile 'junit:junit:4.12'

    if (project.name != 'common') {
      compile project(':common')
    }
  }

  sourceSets {
    main.java.srcDirs += 'src/main/kotlin'
  }
}

task wrapper(type: Wrapper) {
  gradleVersion = '2.13'
}

